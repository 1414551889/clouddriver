apply plugin: 'com.google.protobuf'

buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.5'
  }
}

ext {
  protobufVersion = '3.2.+'
  grpcVersion = '1.9.+'
}

repositories {
  maven { url 'https://dl.bintray.com/netflixoss' }
}

dependencies {
  protobuf 'com.netflix.titus:titus-api-definitions:0.0.1-rc59'

  api "com.google.protobuf:protobuf-java:$protobufVersion"
  api "io.grpc:grpc-protobuf:$grpcVersion"
  api "io.grpc:grpc-stub:$grpcVersion"
  api "io.grpc:grpc-netty-shaded:$grpcVersion"
  api project(":clouddriver-aws")
  api project(":clouddriver-core")
  api project(":clouddriver-eureka")
  api "org.springframework.boot:spring-boot-actuator"
  api "org.springframework.boot:spring-boot-starter-web"
  api "com.squareup.okhttp3:okhttp:3.1.2"

  testImplementation "org.slf4j:slf4j-simple"
  testImplementation "org.spockframework:spock-core"
  testImplementation "org.spockframework:spock-spring"
  testImplementation "org.springframework:spring-test"
  testImplementation "cglib:cglib-nodep"
  testImplementation "org.objenesis:objenesis"
  testImplementation "junit:junit"
  testImplementation "org.hamcrest:hamcrest-core"

}

sourceSets {
  main {
    java {
      srcDir "${protobuf.generatedFilesBaseDir}/main/grpc"
      srcDir "${protobuf.generatedFilesBaseDir}/main/java"
    }
  }
}

task cleanGenerated(type: Delete) {
  delete "$projectDir/src/generated"
}

clean.dependsOn cleanGenerated

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:$protobufVersion"
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc {}
    }
  }
}

javadoc {
  exclude 'io/titanframework/**'
  exclude 'com/netflix/titus/grpc/protogen/**'
}

def licenseExtension = project.extensions.findByName('license')
if (licenseExtension != null) {
  licenseExtension.exclude('io/titanframework/**')
  licenseExtension.exclude('com/netflix/titus/grpc/protogen/**')
}
